;; Composing command lines that don't use any special character.
(defpackage :lisp-invocation/non-special
  (:use :common-lisp :uiop)
  (:export
   #:compose-non-special-string #:compose-copy-paste-string))

(in-package :lisp-invocation/non-special)

(defun non-special-character-p (character)
  "Is this character acceptable in an eval form, even when used under Windows?"
  (let ((code (char-code character)))
    (and (< 32 code 127) (not (find character "'\\\"")))))

(defun non-special-symbol-character-p (character)
  "Is this character acceptable as part of a symbol in an eval form, even when used under Windows?"
  (let ((code (char-code character))) ;; assumes uppercase.
    ;; No "modern" syntax accepted on the test master, though it can be used on the slave.
    (and (< 32 code 91) (not (find character "'\\\":;")))))

(defun need-escape-within-pipe-p (character)
  "Does this character need to be escaped when within symbol pipe notation?"
  (or (eql character #\|) (eql character #\\)))

(defun print-string-in-pipes (string &optional s)
  "Print a symbol |escaped| between pipes with the given STRING as its name, to the output S"
  (with-output (s)
    (princ "|" s)
    (let ((string (string string)))
      (if (some 'need-escape-within-pipe-p string)
          (loop :for c :across string :do
                (when (need-escape-within-pipe-p c) (princ #\\ s))
                (princ c s))
          (princ string s)))
    (princ "|" s)))

(defun compose-non-special-string (forms &optional s)
  "Given some FORMS, compose a string suitable to be passed for evaluation
to a Lisp implementation from the shell command-line with e.g. sbcl --eval or equivalent.
The string will contain no special character (space, dollars, quotes, double quotes)
so that it can be passed as a single argument
in a command-line, without causing munging by a Unix or Windows shell (assuming it's either
quoted or double-quoted).
Output S is as specified as per WITH-OUTPUT.
FORMS may be a preformatted string (assumed to not contain special characters), or
a list of forms, where each form is either a preformatted string,
or a LIST that specifies a program."
  (etypecase forms
    ;; at the toplevel, a string mean "this is pre-formatted", you know what to pass to --eval
    (string
     (output-string forms s))
    (list
     (with-standard-io-syntax
       (let ((*package* (find-package :cl)))
         (with-output (s)
           (labels
               ((p (x) (princ x s)) ;; princ
                (w (x) (write x :stream s :case :downcase)) ;; write
                (c (x) ;; form that evaluates into a character
                  (p "(code-char`") (p (char-code x)) (p ")"))
                (sym (x) ;; print a symbol
                  ;; check that only good characters are used in a symbol;
                  ;; TODO: if not, go through a string (ugh!)
                  (assert (every 'non-special-symbol-character-p (package-name (symbol-package x))))
                  (assert (every 'non-special-symbol-character-p (symbol-name x)))
                  (w x))
                (s (x) ;; form that evaluates into a string
                   (if (every 'non-special-character-p x)
                       (progn ;; simple string literal
                         (p "(string`") (print-string-in-pipes x s) (p ")"))
                       (progn
                         (p "(format()(string`|狺┼á祜镳瑚轸孱戾铉翳烘矧篝狎呼桢ㄩ痫箝糸镱ū痫箝糸镱孱洎烘矧盹蝈篝狎孱洎烘矧痫箝糸镱ㄡ钿盹蝈痫箝糸镱殒铒ь镱箴邈獒飙汨狎徙翦颦后翎螋篝狎舂瑚栝戾盹蝈轰麒孱矧铛祆痫箝糸镱篝狎痫箝糸镱┅痱轭舡篝蜷铉轭痖疱篚怏羼篝狎痫箝糸镱螬麒孱痫箝糸镱ㄣㄣ栳痫箝糸镱┅┅┅┅┅换轶轸翳遽簌汜箦麒弪铒箴徙轶铄邃邃轭翳扉篝祜镳烘矧ㄡ洎猴轰ㄣ镱è铛祆洎蝈趱蝾舂è狒镯洎蝈趱蝾铋飑è矧豉疱Ж矧铛祆汨狎徙翦篝蜷铉泔铙┅ㄡ钿豉疱ㄣ狎洎ъ轶舂ㄣ狎洎┅┅蝈趱蝾铋飑┅┅ㄦ换麸瓠戾鲥骢钽糸镱麸痱轭骘蝽鏖翳秕箴徙弩ㄥ豉疱汜箦铛祆ī┅簌礅镬簌┅蝈犰┅ㄣ栳蜥泗弪．ㄣ┅篝蜷铉痱镧．┅ㄣ镱ㄩ痱镧á磲ī＇┅痱镧．啖ㄢ┅┅┅ㄢ换痱轭骘蝽翳狒麒孱轭箝溴忉汶聃雉瀣弼犰踽翦麸翳骘蝽麇麽铘ㄥ豉疱汜箦铛祆ī┅簌礅镬簌┅蝈犰┅ㄣ栳蜥泗弪ㄣ┅篝蜷铉痱镧┅ㄣ镱á祜镳烘矧ㄡ洎猴轰ㄢ岍ㄣ镱è铛祆洎换娘铄轶沆矬疳蝈è豉疱Ж矧铛祆汨狎徙翦篝蜷铉泔铙┅换娘铄孱潴鏖翳è狒镯洎愧换温麇蝈禊镱忮轭翳筢礤狍轭痱徙糸沐豉疱汜箦ㄣ栳蜥泗弪ㄣ洎篝蜷铉洎蝈犰洎è矧脲黠蜾ㄥ耢舂簌洎⑧ㄢ洎┅è豉疱ㄣ狎洎Ж矧扉篝汨狎徙翦篝蜷铉┅换娘铄篝狎趔鏖翳矧è豉疱ㄣ狎洎Ж矧蝈犰脲黠蜾怙镬遽瞟换轭箦螋忮骘蝈泔铙翎铘┅è豉疱ㄣ狎洎簌礅镬啖┅换轭箦螋忮骘蝈鲠蜷徕戾簌礅镬ㄥ蝌矧㈡镲┅┅┅┅聃雉濞祜镳烘矧骘蝽洪骘蝽轰．ㄥ豉疱汜箦骘蝽篝蜷铉骘蝽┅换骘蝽溟蝈泗禊躅溴翳麸瓠戾鲥汜忮痱彐矧磲趑邃麸扉篝ㄦ骘蝽┅┅┅┅┅┅┅ㄤ彐躅泔眇矬瀛泔瘗疳篝瀛篝蜷铉ㄦ矧眢镳糸镱犰螬⑶轹孱翳筢礤轭瘐狍骘孟托嫌怒蜗苇有琶闪汰釉疑吻秕麴豸翳鲥蝙筢礤葡彝轭桴磲瞽蝈徜徕戾骘蝽翳狒汜忮泔瘗疳篝邃犷邃轸邃镱麸蔑眄镱涕箴遗刑ㄥ豉疱汜箦骘蝽篝蜷铉秕麴豸篝蜷铉骘蝽螬扉篝ㄦ矧磲窿"
                  (with-standard-io-syntax
                    (let ((*package* (find-package :cl))
                          (*print-case* :downcase))
                      (mapcar (lambda (x)
                                (typecase x (string x)
                                          (t (write-to-string x))))
                              forms)))))))
